# Maintainer: Russi Chatterjee <ixaxaar@mailbox.org>
pkgname=mlnx-xpmem
_pkgname=xpmem
pkgver=2.7.3
pkgrel=1
pkgdesc="XPMEM (Cross-Partition Memory) is a Linux kernel module that allows programs to share memory, for Mellanox OFED"
arch=('x86_64')
url="https://github.com/hpc/xpmem"
license=('GPL2')
depends=('linux-headers')
makedepends=('git')
install=xpmem.install
source=("$_pkgname-$pkgver.tar.gz::https://linux.mellanox.com/public/repo/mlnx_ofed/latest/SRPMS/$_pkgname-$pkgver.tar.gz")
sha256sums=('SKIP')

build() {
  cd "$_pkgname-$pkgver"
  ./configure --prefix=/usr
  make
  libtool --finish /usr/lib
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Install udev rules
  install -Dm644 "$pkgdir/lib/udev/rules.d/56-xpmem.rules" \
                 "$pkgdir/usr/lib/udev/rules.d/56-xpmem.rules"
  rm -rf "$pkgdir/lib"

  # Install init script
  install -Dm755 "$pkgdir/usr/etc/init.d/xpmem" \
                 "$pkgdir/etc/init.d/xpmem"
  rm -rf "$pkgdir/usr/etc"

  # Set name and version in dkms.conf
  sed -e "s/@PACKAGE_NAME@/$_pkgname/" \
      -e "s/@PACKAGE_VERSION@/$pkgver/" \
      -i "dkms.conf"

  # Install dkms.conf
  install -Dm644 "dkms.conf" "$pkgdir/usr/src/$_pkgname-$pkgver/dkms.conf"
}
